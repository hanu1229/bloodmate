plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    // Node Gradle 플러그인 추가
    id "com.github.node-gradle.node" version "7.0.1" // 최신 호환 버전 확인 권장
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // 스프링
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    // 롬북
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    // MySql JDBC
    runtimeOnly 'com.mysql:mysql-connector-j'
    // JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    //Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    // JJWT
    // https://mvnrepository.com/artifact/io.jsonwebtoken/jjwt-api
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    // https://mvnrepository.com/artifact/io.jsonwebtoken/jjwt
    implementation 'io.jsonwebtoken:jjwt:0.12.6'
    // https://mvnrepository.com/artifact/io.jsonwebtoken/jjwt-impl
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    // mail
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    // spring security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'
    // AWS S3
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3:3.1.1'




}

tasks.named('test') {
    useJUnitPlatform()
}

// ===================================================
// Node Gradle 플러그인 설정
node {
    version = '22.14.0' // 프로젝트에 맞는 Node.js 버전 지정
    download = true
    npmWorkDir = file("${project.projectDir}/src/main/reactapp") // React 프로젝트 경로
}
// --- React 빌드 관련 Task 정의 ---
// 1. npm install 실행 Task (React 의존성 설치)
task npmInstallReact(type: NpmTask) {
    description = "Install Node.js dependencies from package.json"
    workingDir = file("${project.projectDir}/src/main/reactapp")
    args = ['install']
    inputs.file("src/main/reactapp/package.json")
    inputs.file("src/main/reactapp/package-lock.json")
    outputs.dir("src/main/reactapp/node_modules")
}

// 2. npm run build 실행 Task (React 앱 빌드)
task npmBuildReact(type: NpmTask) {
    description = "Build the React application using npm run build"
    dependsOn npmInstallReact
    workingDir = file("${project.projectDir}/src/main/reactapp")
    args = ['run', 'build']
    inputs.dir("src/main/reactapp/src")
    inputs.dir("src/main/reactapp/public")
    inputs.file("src/main/reactapp/package.json")
    outputs.dir("src/main/reactapp/dist")
}

task copyReactBuild(type: Copy) {
    description = "Copy React build output to Spring Boot static resources"
    dependsOn npmBuildReact
    from "${project.projectDir}/src/main/reactapp/dist"
    into "${project.buildDir}/resources/main/static"
}

processResources {
    dependsOn copyReactBuild
}

clean {
    delete file("${project.projectDir}/src/main/reactapp/node_modules")
    delete file("${project.projectDir}/src/main/reactapp/dist")
}